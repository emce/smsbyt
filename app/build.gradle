apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "com.ofg.uptodate"

def commitCount = Integer.parseInt('git rev-list HEAD --count'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools
    defaultConfig {
        applicationId "mobi.cwiklinski.smsbyt"
        minSdkVersion 16
        targetSdkVersion compileSdk
        versionCode commitCount
        versionName appVersion + versionCode
        vectorDrawables.useSupportLibrary = true
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        test.assets.srcDirs += 'src/main/assets'
        test.resources.srcDirs += 'src/main/res'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable "InvalidPackage"
        disable "UnusedResources"
        lintConfig rootProject.file('gradle/lint.xml')
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "com.google.firebase:firebase-crash:$firebaseVersion"


    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "io.reactivex:rxjava:$rxJavaVersion"
    implementation ("io.reactivex:rxandroid:$rxAndroidVersion") {
        exclude group: "io.reactivex", module: "rxjava"
    }
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation ("com.squareup.retrofit2:retrofit:$retrofitVersion") {
        exclude group: "com.squareup.okhttp3"
    }
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation ("com.squareup.retrofit2:converter-gson:$retrofitVersion") {
        exclude group: "com.squareup.okhttp3"
    }
    implementation ("com.squareup.retrofit2:adapter-rxjava:$retrofitVersion") {
        exclude group: "com.squareup.okhttp3"
        exclude group: "io.reactivex", module: "rxjava"
    }
    implementation ("com.tbruyelle.rxpermissions:rxpermissions:$permissionUtilsVersion@aar") {
        exclude group: "com.android.support", module: "appcompat-v7"
    }
    implementation "org.codejargon.feather:feather:$featherVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation ("io.reactivex:rxkotlin:$rxKotlinVersion") {
        exclude group: "io.reactivex", module: "rxjava"
        exclude group: "org.jetbrains.kotlin"
    }

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testImplementation "org.hamcrest:hamcrest-core:$hamcrestCoreVersion"
    testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
    testImplementation ("com.nhaarman:mockito-kotlin:$mockitoKotlinVersion") {
        exclude group: "org.jetbrains.kotlin"
        exclude group: "org.mockito"
    }
}

repositories {
    mavenCentral()
}
